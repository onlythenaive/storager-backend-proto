buildscript {

    repositories {

        jcenter()
    }

    dependencies {

        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'war'

group 'ru.spb.iac.storager'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {

    jcenter()

    flatDir {
        dirs 'libs'
    }
}

task genJaxb {

    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/integration/providing.xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true, includeantruntime: false,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

bootRun {

    systemProperties = System.properties
}

configurations {
    jaxb
}

war {

    archiveName 'storager-server.war'
}

dependencies {

    compile             group: 'org.springframework.boot',      name: 'spring-boot-starter-web'
    compile             group: 'org.springframework.boot',      name: 'spring-boot-starter-data-jpa'
    compile             group: 'org.springframework.boot',      name: 'spring-boot-starter-web-services'

    compile             group: 'com.github.jknack',             name: 'handlebars',                     version: '4.0.6'
    compile             group: 'com.google.guava',              name: 'guava',                          version: '19.0'
    compile             group: 'org.hibernate',                 name: 'hibernate-java8',                version: '5.1.0.Final'
    compile             group: 'org.hsqldb',                    name: 'hsqldb',                         version: '2.3.1'
    compile             group: 'wsdl4j',                        name: 'wsdl4j',                         version: '1.6.1'

    compile             name: 'ojdbc6'

    providedRuntime     group: 'org.springframework.boot',      name: 'spring-boot-starter-tomcat'

    testCompile         group: 'junit',                         name:  'junit',                         version: '4.11'

    jaxb("com.sun.xml.bind:jaxb-xjc:2.2.4-1")
    compile(files(genJaxb.classesDir).builtBy(genJaxb))
}
