buildscript {

    repositories {

        jcenter()
    }

    dependencies {

        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'war'

group 'ru.spb.iac.storager'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {

    jcenter()
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/integration/providing.xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

bootRun {

    systemProperties = System.properties
}

configurations {
    jaxb
}

war {

    archiveName 'storager-server.war'
    from genJaxb.classesDir
}

dependencies {

    compile     group: 'com.google.guava',          name:  'guava',                        version: '19.0'

    compile     group: 'org.hibernate',             name:  'hibernate-java8',              version: '5.1.0.Final'

    compile     group: 'org.springframework.boot',  name:  'spring-boot-starter-web'
    compile     group: 'org.springframework.boot',  name:  'spring-boot-starter-data-jpa'
    providedRuntime     group: 'org.springframework.boot',      name: 'spring-boot-starter-tomcat'

    compile     group: 'org.hsqldb',                name:  'hsqldb',                       version: '2.3.1'

    testCompile group: 'junit',                     name:  'junit',                        version: '4.11'

    compile("org.springframework.boot:spring-boot-starter-ws")
    compile("wsdl4j:wsdl4j:1.6.1")
    jaxb("com.sun.xml.bind:jaxb-xjc:2.2.4-1")
    compile(files(genJaxb.classesDir).builtBy(genJaxb))
}
